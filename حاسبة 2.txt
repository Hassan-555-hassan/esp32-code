#include <MCUFRIEND_kbv.h>
#include <Adafruit_GFX.h>
#include <TouchScreen.h>

#define YP A1
#define XM A2
#define YM 7
#define XP 6

#define WHITE   0x0000 // Black->White بالعكس حسب تعريفك
#define YELLOW  0x001F // Blue->Yellow
#define CYAN    0xF800 // Red->Cyan
#define PINK    0x07E0 // Green-> Pink
#define RED     0x07FF // Cyan -> Red
#define GREEN   0xF81F // Pink -> Green
#define BLUE    0xFFE0 // Yellow->Blue
#define BLACK   0xFFFF // White-> Black

#define MINPRESSURE 10
#define MAXPRESSURE 1000

// معايرة لمس حسب جهازك:
#define TS_MINX 230
#define TS_MAXX 1016
#define TS_MINY 250
#define TS_MAXY 944

TouchScreen ts = TouchScreen(XP, YP, XM, YM, 300);
MCUFRIEND_kbv tft;

String symbol[4][4] = {
  { "7", "8", "9", "/" },
  { "4", "5", "6", "x" },
  { "1", "2", "3", "-" },
  { "C", "0", "=", "+" }
};

int X, Y;
long Num1 = 0, Num2 = 0, Number = 0;
char action = 0;
boolean result = false;

void setup() {
  Serial.begin(9600);
  uint16_t ID = tft.readID();
  tft.begin(ID);
  tft.setRotation(4);
  tft.fillScreen(WHITE);
  IntroScreen();
  drawButtons();
}

void loop() {
  TSPoint p = waitTouch();
  X = p.x;
  Y = p.y;
  handleTouch();

  if (result) {
    calculateResult();
    showResult();
    result = false;
  }

  delay(300);
}

TSPoint waitTouch() {
  TSPoint p;
  do {
    p = ts.getPoint();
    pinMode(XM, OUTPUT);
    pinMode(YP, OUTPUT);
  } while ((p.z < MINPRESSURE) || (p.z > MAXPRESSURE));

  // عكس الـ Y حسب اتجاه الشاشة، هذا مهم
  int x = map(p.x, TS_MINX, TS_MAXX, 0, 240);
  int y = map(p.y, TS_MINY, TS_MAXY, 320, 0);

  return TSPoint(x, y, p.z);
}

void handleTouch() {
  int adjustedY = Y - 80;
  if (adjustedY < 0) return;

  int row = adjustedY / 60;
  int col = X / 60;

  if (row >= 0 && row < 4 && col >= 0 && col < 4) {
    String val = symbol[row][col];
    Serial.println("Pressed: " + val);

    if (val == "C") {
      Number = Num1 = Num2 = 0;
      action = 0;
      result = false;
      showResult();
    } else if (val == "+") {
      action = '+';
      Num1 = Number;
      Number = 0;
    } else if (val == "-") {
      action = '-';
      Num1 = Number;
      Number = 0;
    } else if (val == "x") {
      action = '*';
      Num1 = Number;
      Number = 0;
    } else if (val == "/") {
      action = '/';
      Num1 = Number;
      Number = 0;
    } else if (val == "=") {
      Num2 = Number;
      result = true;
    } else {
      int digit = val.toInt();
      Number = Number * 10 + digit;
      showResult();
    }
  }
}

void calculateResult() {
  if (action == '+') Number = Num1 + Num2;
  else if (action == '-') Number = Num1 - Num2;
  else if (action == '*') Number = Num1 * Num2;
  else if (action == '/') Number = (Num2 != 0) ? Num1 / Num2 : 0;
}

void showResult() {
  tft.fillRect(0, 0, 240, 80, RED);
  tft.setTextColor(WHITE);
  tft.setTextSize(3);
  tft.setCursor(10, 25);
  tft.print("        "); // لمسح الرقم السابق
  tft.setCursor(10, 25);
  tft.print(Number);
}

void drawButtons() {
  for (int row = 0; row < 4; row++) {
    for (int col = 0; col < 4; col++) {
      int x = col * 60;
      int y = 80 + row * 60;

      uint16_t bgColor = BLACK;  // خلفية الأرقام افتراضياً
      uint16_t txtColor = WHITE; // نص الأزرار باللون الأسود

      if (symbol[row][col] == "C") {
        bgColor = CYAN;    // خلفية حمراء لـ C
        txtColor = WHITE; // نص أبيض
      } else if (symbol[row][col] == "=") {
        bgColor =  RED;   // خلفية أزرق سماوي لـ =
        txtColor = WHITE; // نص أبيض
      } else if (symbol[row][col] == "+" || symbol[row][col] == "-" || symbol[row][col] == "x" || symbol[row][col] == "/") {
        bgColor = BLUE;   // خلفية زرقاء للأزرار الحسابية
        txtColor = WHITE; // نص أبيض
      }

      tft.fillRect(x, y, 60, 60, bgColor);
      tft.drawRect(x, y, 60, 60, WHITE);
      tft.setTextColor(txtColor);
      tft.setTextSize(3);
      tft.setCursor(x + 15, y + 15);
      tft.print(symbol[row][col]);
    }
  }
  showResult();
}

void IntroScreen() {
  tft.fillScreen(WHITE);
  tft.setCursor(55, 110);
  tft.setTextSize(3);
  tft.setTextColor(RED);
  tft.println("hassan");
  tft.setCursor(30, 140);
  tft.println("CALCULATOR");
  tft.setCursor(50, 220);
  tft.setTextSize(2);
  tft.setTextColor(BLUE);
  tft.println("Hassan tqatqa");
  tft.setCursor(30, 250);
  tft.setTextColor(CYAN);
  tft.println("YouTube Channel");
  delay(4000);
}
